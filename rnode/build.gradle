group 'org.fruttech.rendering.fruttech'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'idea'


repositories {
    mavenCentral()
}

configurations {
    provided
    compileWithLogs
    compile.extendsFrom provided
}

dependencies {

    //GOOGLE Guice
    compile group: 'com.google.inject', name: 'guice', version: '4.0'
    compile group: 'com.google.inject.extensions', name: 'guice-multibindings', version: '4.0'

    //Hazelcast
    compile group: 'com.hazelcast', name: 'hazelcast', version: '3.5.3'

    //Kafka
    compile group: 'org.apache.kafka', name: 'kafka-clients', version: '0.8.2.2'
    compile group: 'org.apache.kafka', name: 'kafka_2.9.1', version: '0.8.2.2'

    //Storm core
    provided(group: 'org.apache.storm', name: 'storm-core', version: '0.10.0') {
        exclude module: 'log4j-over-slf4j'
    }
    compile group: 'org.apache.storm', name: 'storm-kafka', version: '0.10.0'

    //common libs
    compile group: 'com.google.code.gson', name: 'gson', version: '2.4'
    compile(group: 'org.slf4j', name: 'slf4j-api', version: '1.7.5') {
        exclude module: 'log4j-over-slf4j'
    }
    compile group: 'commons-cli', name: 'commons-cli', version: '1.3.1'

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.5'

    compileWithLogs group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.5'
    compileWithLogs group: 'log4j', name: 'log4j', version: '1.+'

}

jar {
    manifest {
        attributes 'Implementation-Title': 'Netrender CliClient',
                'Implementation-Version': 1.0,
                'Main-Class': 'org.fruttech.rendering.CliClient'
    }

    dependsOn configurations.runtime

    from {
        (configurations.runtime - configurations.provided).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    from {
        configurations.compileWithLogs.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

task stormLocalJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Netrender RenderingTopology',
                'Implementation-Version': 1.0,
                'Main-Class': 'org.fruttech.rendering.storm.RenderingTopology'
    }

    appendix 'storm-local'
    dependsOn configurations.runtime

    from sourceSets.main.output
    from {
        configurations.runtime.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}


task stormJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Netrender RenderingTopology',
                'Implementation-Version': 1.0,
                'Main-Class': 'org.fruttech.rendering.storm.RenderingTopology'
    }

    appendix 'storm-cluster'
    dependsOn configurations.runtime

    from sourceSets.main.output
    from {
        (configurations.runtime - configurations.provided).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}
